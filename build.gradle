/*
 * ProxyDiscord, a Velocity Discord bot
 * Copyright (c) 2021 James Lyne
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

rootProject.ext.mavenUserName=""
rootProject.ext.mavenPassword=""

if (file("./build.gradle.local").exists()) {
    apply from: "./build.gradle.local"
}

sourceSets {
    main {
        java {
            srcDirs= ["./src/java"]
        }
        resources {
            srcDirs= ["./src/resources"]
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.not-null.co.uk/releases/' }
        maven { url "https://papermc.io/repo/repository/maven-public/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots" }
    }

    group = 'uk.co.notnull'
    version = '1.2-SNAPSHOT'
    description = 'Velocity Discord integration solution'

    java.sourceCompatibility = JavaVersion.VERSION_17
    java.targetCompatibility = JavaVersion.VERSION_17

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }
}

dependencies {
    implementation project(":ProxyDiscordAPI")
    implementation 'uk.co.notnull:javacord:3.8.0'
    implementation 'cloud.commandframework:cloud-velocity:1.8.3'
    implementation 'cloud.commandframework:cloud-minecraft-extras:1.8.3'
    implementation 'cloud.commandframework:cloud-annotations:1.8.3'
    implementation 'dev.vankka:mcdiscordreserializer:4.4.0-SNAPSHOT'
    implementation 'uk.co.notnull:SuperVanishBridge-helper:1.0'

    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    compileOnly 'uk.co.notnull:PlatformDetection-velocity:2.1'

    annotationProcessor 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
}

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    build {
        dependsOn(shadowJar)
    }

    shadowJar {
        archiveClassifier.set('')
        relocate 'cloud.commandframework', 'uk.co.notnull.proxydiscord.shaded.cloud'
        relocate 'io.leangen.geantyref', 'uk.co.notnull.proxydiscord.shaded.typetoken'
        relocate 'uk.co.notnull.SuperVanishBridge', 'uk.co.notnull.proxydiscord.shaded.supervanishbridge'
    }
}
